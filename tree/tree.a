10 REM PROMPT USER FOR ANGLE
20 TEXT : HOME
30 PRINT "I WILL DRAW YOU A TREE!"
40 PRINT "ENTER AN ANGLE IN RADIANS"
50 INPUT DA

60 REM HIGH RESOLUTION GRAPHICS
70 HGR
80 HCOLOR = 3

90 REM INITIAL VARIABLES
100 SHRINK = 0.67
110 A = 3.14159 * 0.5
120 L = 50
130 LEVEL = 0
140 MAXLEV = 10
150 X = 140 : Y = 159

160 REM ARRAY FOR STACK OF TREE POSITIONS
170 DIM XS(MAXLEV) : DIM YS(MAXLEV)

180 REM START BRANCHING
190 GOSUB 1000
200 END

1000 REM DRAW A SINGLE BRANCH!

1010 REM CALCULATE NEXT POINT
1020 DX = L * COS(A)
1030 DY = L * SIN(A)
1040 NX = X + DX : NY = Y - DY

1050 REM DRAW BRANCH
1060 HPLOT X, Y TO NX, NY

1070 REM SAVE POSITION
1080 XS(LEVEL) = X : YS(LEVEL) = Y

1090 REM NEXT BRANCH
1100 X = NX : Y = NY
1110 LEVEL = LEVEL + 1
1120 A = A + DA
1130 L = L * SHRINK
1140 IF LEVEL < MAXLEV THEN GOSUB 1000

1150 REM AND ANOTHER BRANCH
1160 A = A - DA * 2
1170 IF LEVEL < MAXLEV THEN GOSUB 1000

1180 REM POP BACK
1190 A = A + DA
1200 L = L / SHRINK
1210 LEVEL = LEVEL - 1
1220 X = XS(LEVEL) : Y = YS(LEVEL)
1230 RETURN