10 TEXT : HOME
20 PRINT "I WILL DRAW YOU A TREE!"
30 PRINT "ENTER AN ANGLE IN RADIANS"
40 INPUT DA
50 HGR
60 HCOLOR = 3
70 SHRINK = 0.67
80 A = 3.14159 * 0.5
90 L = 50
100 LEVEL = 0
110 MAXLEV = 10
120 X = 140 : Y = 159
130 DIM XS(MAXLEV) : DIM YS(MAXLEV)
140 GOSUB 1000
150 END

1000 REM DRAW A SINGLE BRANCH!
1010 DX = L * COS(A)
1020 DY = L * SIN(A)
1030 NX = X + DX : NY = Y - DY
1040 HPLOT X,Y TO NX, NY
1050 XS(LEVEL) = X : YS(LEVEL) = Y
1060 X = NX : Y = NY
1070 LEVEL = LEVEL + 1
1080 A = A + DA
1090 L = L * SHRINK
1100 IF LEVEL < MAXLEV THEN GOSUB 1000
1110 A = A - DA * 2
1120 IF LEVEL < MAXLEV THEN GOSUB 1000
1130 A = A + DA
1140 L = L / SHRINK
1150 LEVEL = LEVEL - 1
1160 X = XS(LEVEL) : Y = YS(LEVEL)
1170 RETURN