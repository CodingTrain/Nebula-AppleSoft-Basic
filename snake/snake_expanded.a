10 HOME
20 LET L = 1               : REM SNAKE STARTS WITH LENGTH 1
30 LET W = 40 : LET H = 24 : REM WIDTH AND HEIGHT
40 GOSUB 2000              : REM INITIALIZE FOOD
50 DIM X(100) : DIM Y(100) : REM INTIALIZE ARRAY
60 X(L) = 20 : Y(L) = 12 

65 REM MAIN LOOP
70 GOSUB 3000
80 FOR N = 1 TO 1000 - L * 2 : NEXT N
90 GOTO 70

999  REM READ KEYBOARD INPUT
1000 LET K = PEEK(49152)
1010 IF K = 196 THEN X(L) = X(L) + 1
1020 IF K = 193 THEN X(L) = X(L) - 1
1030 IF K = 215 THEN Y(L) = Y(L) - 1
1040 IF K = 211 THEN Y(L) = Y(L) + 1
1050 IF X(L) < 1 OR X(L) > W THEN GOTO 6000
1060 IF Y(L) < 1 OR Y(L) > H THEN GOTO 6000
1070 IF L > 1 THEN GOSUB 7000
1080 RETURN

1990 REM CREATE FOOD
2000 LET FX = INT(RND(1) * (W - 1)) + 1
2010 LET FY = INT(RND(1) * (H - 1)) + 1
2020 HTAB FX : VTAB FY
2030 PRINT "F"
2040 RETURN

2990 REM DISPLAY SNAKE
3000 HTAB X(1) : VTAB Y(1)
3010 PRINT " " 
3030 IF X(1) = FX AND Y(1) = FY THEN GOSUB 8000
3040 IF L > 1 THEN GOSUB 5000
3050 GOSUB 1000
3060 IF X(L) = FX AND Y(L) = FY THEN GOSUB 4000
3070 HTAB X(L) : VTAB Y(L)
3080 PRINT "S"
3090 RETURN

3990 REM GROW SNAKE
4000 L = L + 1
4010 X(L) = X(L - 1)
4020 Y(L) = Y(L - 1)
4030 GOSUB 2000
4040 RETURN

4990 REM SHIFT SNAKE ARRAY
5000 FOR I = 1 TO L - 1
5010 X(I) = X(I + 1)
5020 Y(I) = Y(I + 1)
5030 NEXT I
5040 RETURN

6000 REM GAME OVER
6010 HOME
6020 HTAB 15 : VTAB 10
6030 PRINT "GAME OVER!"
6040 HTAB 13 : VTAB 11 : PRINT "YOUR SCORE WAS "
6050 HTAB 20 : VTAB 12 : PRINT L
6060 END

7000 REM CHECK IF SNAKE HIT ITSELF
7010 FOR I = 1 TO L-1
7020 IF X(L) = X(I) AND Y(L) = Y(I) GOTO 6000
7030 NEXT I
7040 RETURN

8000 REM REDRAW FOOD
8010 HTAB FX : VTAB FY
8020 PRINT "F"
8030 RETURN
